<?xml version="1.0" encoding="utf-8"?>
<!--
https://lib.bgsu.edu/exlibris/
Author: John Kloor <kloor@bgsu.edu>
Revised: 2025-02-07

This file provides a means to use the header.xml, footer.xsl and style.xsl
templates with records emailed from Primo. By default, it will pass through
the email generated by Primo as-is. But, if there is content in the generated
email between the unescaped versions of the strings in the variables after and
before, that content will be sent instead.

The content to be sent is to be placed in a main element, e.g. between a
<main> and a </main> tag. Any content outside of those tags will be discarded.
The contents will be placed within a new HTML document, including the
generalStyle and bodyStyleCss templates from style.xml, the head template from
header.xsl and the lastFooter template from footer.xsl.
-->
<xsl:stylesheet version="2.0" xmlns:xsl="http://www.w3.org/1999/XSL/Transform">
    <xsl:include href="header.xsl"/>
    <xsl:include href="footer.xsl"/>
    <xsl:include href="style.xsl"/>
	<xsl:template match="/">
        <xsl:variable name="after" select="'&lt;main&gt;'"/>
        <xsl:variable name="before" select="'&lt;/main&gt;'"/>
        <xsl:variable name="source" select="/notification_data/metadata/node()"/>
        <xsl:variable name="parsed" select="substring-before(substring-after($source, $after), $before)"/>
    	<xsl:choose>
            <xsl:when test="$parsed=''">
        		<xsl:value-of select="$source" disable-output-escaping="yes"/>
            </xsl:when>
            <xsl:otherwise>
                <html>
                    <xsl:if test="notification_data/languages/string">
                        <xsl:attribute name="lang">
                            <xsl:value-of select="notification_data/languages/string"/>
                        </xsl:attribute>
                    </xsl:if>
                    <head>
                        <title>
                            <xsl:value-of select="notification_data/general_data/subject"/>
                        </title>
                        <xsl:call-template name="generalStyle"/><!-- style.xsl -->
                    </head>
                    <body>
                        <xsl:attribute name="style">
                            <xsl:call-template name="bodyStyleCss"/><!-- style.xsl -->
                        </xsl:attribute>
                        <xsl:call-template name="head"/><!-- header.xsl -->
                        <xsl:value-of select="$parsed" disable-output-escaping="yes"/>
                        <xsl:call-template name="lastFooter"/><!-- footer.xsl -->
                    </body>
                </html>
            </xsl:otherwise>
        </xsl:choose>
	</xsl:template>
</xsl:stylesheet>
